operation Interaction hasOperation(op : Operation) : Boolean {
	for(message in self.message){
		if(message.signature == op){
			return true;
		}
	}
	return false;
}

operation Interaction hasComponent(component : Component) : Boolean {
	for(property in self.ownedAttribute){
		if(property.type == component){
			return true;
		}
	}
	return false;
}

operation Interaction getIndexOf(fragment : InteractionFragment) : Integer{
	var indexOf : Integer = self.getFragments()->indexOf(fragment);
	return indexOf;
}

operation Interaction getIndexOf(op : Operation) : List(Integer){
	var returnValue : List(Integer) = new List(Integer);
	for (mes in self.getMessages()) { 
		
		if( mes.getSignature() == op ){
			returnValue.add(self.getIndexOf(mes.getSendEvent()));
		}
	}
	return returnValue;
}

operation InteractionOperand getIndexOf(fragment : InteractionFragment) : Integer{

	var indexOf : Integer = self.getFragments()->indexOf(fragment);
	return indexOf;
}

operation Interaction addFragmentAt(fragment : InteractionFragment, indexOf : Integer){
	self.getFragments().add(indexOf,fragment);
}

operation Interaction removeLifelineOf(component : Component) {
	self.getLifelines().remove(component.getLifeline(self));
}


operation Interaction getClassifierLifeline(classifier : Classifier) : Lifeline{
	
	var lifelines = Lifeline.allInstances->select(lf | lf.getInteraction() == self);
	
	for(lf in lifelines){
		if(lf.getRepresents().getType() == classifier)
			return lf;
	}
}

operation Interaction getComponentLifeline(component : Component) : Lifeline{
	for(lf in self.getLifelines()){
		var represents = lf.getRepresents();
		for(property in component.getAllInteractionProperties())
			if(property == represents)
				return lf;
	}
	return null;
}

operation Interaction getClassLifeline(class : Class) : Lifeline{
	var lifelines = Lifeline.allInstances->select(lf | lf.getInteraction() == self);
	for(lf in lifelines){
		if(lf.getRepresents().getType() == class)
			return lf;
	}
}

operation Interaction getUseCase() : UseCase{
	return self.getNamespace();
}


//messaggi in uscita da una lifeline
operation Interaction getMessageOf(component : Component) : Sequence(Message){
	var returnValue : Sequence(Message) = new Sequence(Message);
	var componentLf = self.getComponentLifeline(component);
	
	if(componentLf.isUndefined()){
		"there was an error!!!".println();
		return null;
	}
	
	for (mes in self.getMessages()) { 
		
		if( componentLf.getCoveredBys()->exists(iterator | iterator == mes.getSendEvent()) ){
			returnValue.add(mes);
		}
	}
	return returnValue;
}

operation Interaction getMessageOf(classifier : Classifier) : Sequence(Message){
	var returnValue : Sequence(Message) = new Sequence(Message);
	var classifierLf = self.getClassifierLifeline(classifier);
	for (mes in self.getMessages()) { 
		if( classifierLf.getCoveredBys()->exists(iterator | iterator == mes.getSendEvent()) ){
			returnValue.add(mes);
		}
	}
	return returnValue;
}

operation Interaction getLifeline(mos : MessageOccurrenceSpecification) : Lifeline{
	return Lifeline.allInstances->selectOne(lf | lf.namespace == self and 
			lf.getCoveredBys()->exists(iterator | iterator == mos));
	//TODO 
	//metodo getLifelines con modello di papyrus non funziona
	/*for(lf in self.getLifelines()){
		if(lf.getCoveredBys()->exists(iterator | iterator == mos))
			return lf;
	}
	return null;*/
}

operation Interaction createLifelineOf(component : Component) : Lifeline {
	var lifeline = self.createLifeline(component.getName()+"_Lifeline");
	var componentRepresents = self.createOwnedAttribute(component.getName(), component);
	lifeline.setRepresents(componentRepresents);
	return lifeline;
}

operation Interaction createLoopFragmentAt(index : Integer) : CombinedFragment{
	var loopFragment : CombinedFragment = new CombinedFragment();
	self.addFragmentAt(loopFragment, index);
	loopFragment.setName("LoopFragment_Decomposition");
	//loopFragment.setInteractionOperand(Native("InteractionOperatorKind").LOOP);
	var operand = loopFragment.createOperand(loopFragment.getName()+"_Operand");
	var guard = operand.createGuard(operand.getName()+"_Guard");
	return loopFragment;
}

